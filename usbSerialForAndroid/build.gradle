plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        consumerProguardFiles 'proguard-rules.pro'
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [                    // Raspi   Windows   LinuxVM   ...
                'rfc2217_server_host': '192.168.0.78',
                'rfc2217_server_nonstandard_baudrates': 'true',   // true    false     false
        ]
/*
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
                abiFilters 'arm64-v8a'
            }
        }

 */
    }
    /*
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.22.1"
        }
    }*/
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'com.hoho.android.usbserial'
    publishing {
        // if coverage is enabled, change 'release' to 'anyDeviceRelease' or comment out publishing rule
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.8.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'commons-net:commons-net:3.10.0'
    androidTestImplementation 'org.apache.commons:commons-lang3:3.14.0'
}

// gradle task: publishToMavenLocal
project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                // values used for local maven repo, jitpack uses github release:
                groupId 'com.github.mik3y'
                artifactId 'usb-serial-for-android'
                version '3.8.0beta'
            }
        }
    }
}

//apply from: 'coverage.gradle'
